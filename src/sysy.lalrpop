use lalrpop_util::ErrorRecovery;

use crate::ast::*;

#[LALR]
grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, &'static str>>);

match {
  // skip blanks and comments
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  // default case
  _
}
pub CompUnit: CompUnit = <items: GlobalItem+> => CompUnit { <> };

GlobalItem: GlobalItem = {
  Decl => GlobalItem::Decl(<>),
  FuncDef => GlobalItem::Func(<>),
};

FuncDef: FuncDef = {
  <ret_ty: Type> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { ret_ty, ident, params: vec![], block }
  },
  <ret_ty: Type> <ident: Ident> "(" <params: FuncParams> ")" <block: Block> => {
    FuncDef { <> }
  }
};

FuncParams: Vec<FuncParam> = <Comma<FuncParam>> => <>;

FuncParam: FuncParam = "int" <ident: Ident> => FuncParam { ty: Type::Int, ident };

Block: Block = "{" <BlockItem*> "}" => Block::new_with_vec(<>);

BlockItem: BlockItem = {
  Decl => BlockItem::Decl(<>),
  Stmt => BlockItem::Stmt(<>),
};

// Declaration
Decl: Decl = {
  ConstDecl => Decl::ConstDecl(<>),
  VarDecl => Decl::VarDecl(<>),
}

VarDecl: Vec<VarDecl> = {
  <ty: Type> <defs: Comma<VarDef>> ";" => {
    if !matches!(ty, Type::Int) {
      panic!("incomplete type is not allowed");
    }
    defs
  }
};

ConstDecl: Vec<ConstDecl> = {
  "const" <ty: Type> <defs: Comma<ConstDef>> ";" => {
    if !matches!(ty, Type::Int) {
      panic!("incomplete type is not allowed");
    }
    defs
  }
};

VarDef: VarDecl = <lval: LVal> <init: ("=" <InitVal>)?> => VarDecl::new(lval, init);

ConstDef: ConstDecl = <lval: LVal> "=" <init: InitVal> => ConstDecl::new(lval, init);

InitVal: InitVal = {
  Exp => InitVal::Exp(<>),
  "{" <Comma<InitVal>> "}" => InitVal::List(<>),
};

// Statement
Stmt: Stmt = {
  OpenStatement => <>,
  ClosedStatement => <>,
};

NonIfStatement: Stmt = {
  <lval: LVal> "=" <val: Exp> ";" => Stmt::Assign(Assign { lval, val }),
  Block => Stmt::Block(<>),
  <Exp?> ";" => Stmt::Exp(<>),
  "return" <Exp?> ";" => Stmt::Return(Return { ret_val: <> }),
  "continue" ";" => Stmt::Continue(Continue),
  "break" ";" => Stmt::Break(Break),
};

OpenStatement: Stmt = {
  "if" <cond: Condition> <stmt: Stmt> => Stmt::Branch(Branch {
    cond,
    if_stmt: Box::new(stmt),
    el_stmt: None,
  }),

  "if" <cond: Condition> <if_stmt: ClosedStatement> 
  "else" <el_stmt: OpenStatement> => Stmt::Branch(Branch {
    cond,
    if_stmt: Box::new(if_stmt),
    el_stmt: Some(Box::new(el_stmt)),
  }),

  "while" <cond: Condition> <stmt: OpenStatement> => Stmt::While(While {
    cond,
    stmt: Box::new(stmt),
  }),
};

ClosedStatement: Stmt = {
  NonIfStatement => <>,

  "if" <cond: Condition> <if_stmt: ClosedStatement> 
  "else" <el_stmt: ClosedStatement> => Stmt::Branch(Branch {
    cond,
    if_stmt: Box::new(if_stmt),
    el_stmt: Some(Box::new(el_stmt)),
  }),

  "while" <cond: Condition> <stmt: ClosedStatement> => Stmt::While(While {
    cond,
    stmt: Box::new(stmt),
  }),
};

LVal: LVal = <ident: Ident> <dims: Dim*> => LVal {<>};

Dim: Exp = "[" <Exp> "]" => <>;

// Expressions
Exp: Exp = {
  LOrExp => <>,
};

PrimaryExp: Exp = {
  "(" <Exp> ")" => <>,
  IntConst => Exp::Integer(<>),
  LVal => Exp::LVal(<>),
  ! => { errors.push(<>); Exp::Error },
};

UnaryExp: Exp = {
  PrimaryExp => <>,
  Call => Exp::Uxp(UnaryExp::Call(<>)),
  <op: UnaryOp> <uxp: UnaryExp> => Exp::Uxp(UnaryExp::Unary(op, Box::new(uxp))),
};

Call: Call = {
  <func_id: Ident> "(" ")" => Call { func_id, args: vec![] },
  <func_id: Ident> "(" <args: Comma<Exp>> ")" => Call { func_id, args },
};

// macro for all the binary expressions
Tier<Op, NextTier>: Exp = {
  <l: Tier<Op, NextTier>> <op: Op> <r: NextTier> => {
    Exp::Bxp(BinaryExp { op: op, lhs: Box::new(l), rhs: Box::new(r) })
  },
  NextTier
};

MulExp = Tier<MulOp, UnaryExp>;
AddExp = Tier<AddOp, MulExp>;
RelExp = Tier<RelOp, AddExp>;
EqExp = Tier<EqOp, RelExp>;
LAndExp = Tier<LAndOp, EqExp>;
LOrExp = Tier<LOrOp, LAndExp>;

// Operators
MulOp: BinaryOp = {
  "*" => BinaryOp::Mul,
  "/" => BinaryOp::Div,
  "%" => BinaryOp::Mod,
};

AddOp: BinaryOp = {
  "+" => BinaryOp::Add,
  "-" => BinaryOp::Sub,
};

RelOp: BinaryOp = {
  "<" => BinaryOp::Lt,
  "<=" => BinaryOp::Le,
  ">" => BinaryOp::Gt,
  ">=" => BinaryOp::Ge,
};

EqOp: BinaryOp = {
  "==" => BinaryOp::Eq,
  "!=" => BinaryOp::Neq,
};

LAndOp: BinaryOp = {
  "&&" => BinaryOp::And,
};

LOrOp: BinaryOp = {
  "||" => BinaryOp::Or,
};

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Nop,
  "-" => UnaryOp::Neg,
  "!" => UnaryOp::Not,
};

IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
};

// identifier
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

#[inline]
Condition: Exp = "(" <Exp> ")" => <>;

#[inline]
Comma<T>: Vec<T> = {
  <mut items: (<T> ",")*> <item: T> => {
    items.push(item);
    items
  }
};

#[inline]
Type: Type = {
  "int" => Type::Int,
  "void" => Type::Void,
};
