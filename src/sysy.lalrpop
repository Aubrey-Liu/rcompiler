use crate::ast::*;
use std::rc::Rc;

grammar;

match {
  // skip blanks and comments
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*([^\n\r]*[\n\r]*)*\*/" => {},
  // default case
  _
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
};

FuncType: FuncType = "int" => FuncType("int".to_string());

Block: Block = "{" <BlockItem+> "}" => Block::new_with_vec(<>);

BlockItem: AstValue = {
  Return => <>,
  Stmt => <>,
  Decl => <>,
};

Stmt: AstValue = {
  <name: LVal> "=" <exp: Exp> ";" => AstValue::Stmt(Stmt { name: name, val: exp })
};

Decl: AstValue = {
  ConstDecl => <>,
  VarDecl => <>,
};

VarDecl: AstValue = {
  "int" <mut defs: (<VarDef> ",")*> <def: VarDef> ";" => {
    defs.push(def);
    AstValue::Decl(defs)
  }
};

VarDef: Decl = {
  Ident => Decl::new_without_init(<>),
  <name: Ident> "=" <exp: Exp> => Decl::new_with_init(name, exp),
};

ConstDecl: AstValue = {
  "const" "int" <mut defs: (<ConstDef> ",")*> <def: ConstDef>  ";" => {
    defs.push(def);
    AstValue::ConstDecl(defs)
  }
};

ConstDef: ConstDecl = <name: Ident> "=" <init: Exp> => {
  ConstDecl { name: name, init: init }
};

Return: AstValue = "return" <Exp> ";" => AstValue::Return(<>);

// identifier
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
};

LVal: String = Ident => <>;

PrimaryExp: Rc<Exp> = {
  "(" <Exp> ")" => <>,
  IntConst => Rc::new(Exp::Integer(<>)),
  LVal => Rc::new(Exp::LVal(<>, None)),
};

// macro for all the binary expression patterns
Tier<Op, NextTier>: Rc<Exp> = {
  <l: Tier<Op, NextTier>> <op: Op> <r: NextTier> => {
    Rc::new(Exp::Bxp(BinaryExp { op: op, lhs: l, rhs: r }))
  },
  NextTier
};

MulExp = Tier<MulOp, UnaryExp>;
AddExp = Tier<AddOp, MulExp>;
RelExp = Tier<RelOp, AddExp>;
EqExp = Tier<EqOp, RelExp>;
LAndExp = Tier<LAndOp, EqExp>;
LOrExp = Tier<LOrOp, LAndExp>;

// match all the operators
MulOp: BinaryOp = {
  "*" => BinaryOp::Mul,
  "/" => BinaryOp::Div,
  "%" => BinaryOp::Mod,
};

AddOp: BinaryOp = {
  "+" => BinaryOp::Add,
  "-" => BinaryOp::Sub,
};

RelOp: BinaryOp = {
  "<" => BinaryOp::Lt,
  "<=" => BinaryOp::Lte,
  ">" => BinaryOp:: Gt,
  ">=" => BinaryOp::Gte,
};

EqOp: BinaryOp = {
  "==" => BinaryOp::Eq,
  "!=" => BinaryOp::Neq,
};

LAndOp: BinaryOp = {
  "&&" => BinaryOp::And,
};

LOrOp: BinaryOp = {
  "||" => BinaryOp::Or,
};

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Nop,
  "-" => UnaryOp::Neg,
  "!" => UnaryOp::Not,
};

Exp: Rc<Exp> = {
  LOrExp => <>,
};

UnaryExp: Rc<Exp> = {
  PrimaryExp => <>,
  <op: UnaryOp> <uxp: UnaryExp> => {
    Rc::new(Exp::Uxp(UnaryExp { op: op, rhs: uxp }))
  },
};
